/*
 * Since we use the Eclipse Style layout where sources and resources
 * live in the same folders, we need to make some adjustments to Gradle'
 * defaults.
 */
sourceSets {
	main {
		java.srcDirs = ['src', 'xtend-gen', 'src-gen']
		resources {
			srcDirs = ['src', 'src-gen']
			exclude (
				'**/*._trace',
				'**/*.xtendbin',
				'**/*.xtend', 
				'**/*.mwe2',
			)
		}
	}
}

jar {
	metaInf {
		from('META-INF')
	}
}

plugins.withId('eclipse') {
	if (file('META-INF/MANIFEST.MF').exists()) {
		eclipse.classpath.containers 'org.eclipse.pde.core.requiredPlugins'
	}
	/**
	 * Workaround for https://discuss.gradle.org/t/eclipse-classpath-error-when-java-test-code-lives-with-main-code/6547/3
	 */
	eclipse.classpath.file.whenMerged { classpath ->
		classpath.entries.findAll { it.kind == 'src' && !it.path.startsWith('/')}.each {it.excludes = []}
		classpath.entries = classpath.entries.unique()
	}
	eclipseProject.enabled = false
	eclipseJdt.enabled = false
}

apply from: "$rootDir/gradle/unroll-dependencies.gradle"